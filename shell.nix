{ nixpkgs ? ./pinned.nix,
  overlays ? [ (import ./overlay.nix) ],
  pkgs ? import nixpkgs {
    config = {};
    inherit overlays;
  },
  # Will create a temp one if none is passed, for example:
  # nix-shell --argstr buildpath .
  buildpath ? "",

  # The unikernel to build
  unikernel ? "./example"
}:
pkgs.mkShell rec {
  includeos = pkgs.pkgsIncludeOS.includeos;
  stdenv = pkgs.pkgsIncludeOS.stdenv;

  packages = [
    stdenv.cc
    pkgs.buildPackages.cmake
    pkgs.buildPackages.nasm
  ];


  buildInputs = [
    pkgs.microsoft_gsl
  ];

  bootloader="${includeos}/boot/bootloader";

  shellHook = ''
    CC=${stdenv.cc}/bin/clang
    CXX=${stdenv.cc}/bin/clang++
    unikernel=$(realpath ${unikernel})
    echo -e "Attempting to build unikernel: \n$unikernel"
    if [ ! -d "$unikernel" ]; then
        echo "$unikernel is not a valid directory"
        exit 1
    fi
    export BUILDPATH=${buildpath}
    if [ -z "${buildpath}" ]; then
        export BUILDPATH=$(mktemp -d)
        pushd $BUILDPATH
    else
        mkdir -p $BUILDPATH
        pushd $BUILDPATH
    fi
    cmake $unikernel -DARCH=x86_64 -DINCLUDEOS_PACKAGE=${includeos}
    make -j12
    echo -e "\n====================== IncludeOS nix-shell ====================="
    if [ -z "${buildpath}" ]; then
        echo -e "\nWorking directory, generated by this script:"
        echo $BUILDPATH
        echo -e "\nTo use another directory pass in 'buildpath' to nix:"
        echo "nix-shell --argstr buildpath you/build/path"
    fi
    echo -e "\nThe C++ compiler set to:"
    echo $(which $CXX)
    echo -e "\nIncludeOS package:"
    echo ${includeos}
  '';
}
